subroutine gdtost(a,ix,iy,stax,stay,staval)

! Subroutine to return stations back-interpolated values(staval)
!    from uniform grid points using overlapping-quadratics.
! Gridded values of input array a dimensioned a(ix,iy),where
!    ix=grid points in x, iy = grid points in y.  
! Station location given in terms of grid relative station x (stax)
!    and station column.
! Values greater than 1.0e30 indicate missing data.

real :: a(ix,iy),r(4),scr(4)
iy1=int(stay)-1
iy2=iy1+3
ix1=int(stax)-1
ix2=ix1+3
staval=1e30
fiym2=float(iy1)-1
fixm2=float(ix1)-1
ii=0
do i=ix1,ix2
   ii=ii+1
   if (i .ge. 1 .and. i .le. ix) then 
      jj=0
      do j=iy1,iy2
         jj=jj+1
         if (j .ge. 1 .and. j .le. iy) then
            r(jj)=a(i,j)
         else
            r(jj)=1e30
         endif
      enddo   
      yy=stay-fiym2
      if (yy .eq. 2.0) then
         scr(ii)=r(2)
      else
         call binom(1.,2.,3.,4.,r(1),r(2),r(3),r(4),yy,scr(ii))
      endif
   else
      scr(ii)=1e30
   endif
enddo
xx=stax-fixm2
if (xx .eq. 2.0) then
   staval=scr(2)
else
   call binom(1.,2.,3.,4.,scr(1),scr(2),scr(3),scr(4),xx,staval)
endif

return
end

!===============================================================================

subroutine binom(x1,x2,x3,x4,y1,y2,y3,y4,xxx,yyy)

yyy=1e30
if (x2 .gt. 1.e19 .or. x3 .gt. 1.e19 .or.     &
    y2 .gt. 1.e19 .or. y3 .gt. 1.e19) return

wt1=(xxx-x3)/(x2-x3)
wt2=1.0-wt1

if (y4 .lt. 1.e19 .and. x4 .lt. 1.e19) then
   yz22=(xxx-x3)*(xxx-x4)/((x2-x3)*(x2-x4))
   yz22=wt1*(xxx-x4)/(x2-x4)
   yz23=(xxx-x2)*(xxx-x4)/((x3-x2)*(x3-x4))
   yz23=wt2*(xxx-x4)/(x3-x4)
   yz24=(xxx-x2)*(xxx-x3)/((x4-x2)*(x4-x3))
else
   yz22=wt1
   yz23=wt2
   yz24=0.0
endif

if (y1 .lt. 1.e19 .and. x1 .lt. 1.e19) then
   yz11=(xxx-x2)*(xxx-x3)/((x1-x2)*(x1-x3))
   yz12=(xxx-x1)*(xxx-x3)/((x2-x1)*(x2-x3))
   yz12=wt1*(xxx-x1)/(x2-x1)
   yz13=(xxx-x1)*(xxx-x2)/((x3-x1)*(x3-x2))
   yz13=wt2*(xxx-x1)/(x3-x1)
else
   yz11=0.0
   yz12=wt1
   yz13=wt2
endif

if (yz11 .eq. 0. .and. yz24 .eq. 0.) then
   yyy=wt1*y2+wt2*y3
else
   yyy=wt1*(yz11*y1+yz12*y2+yz13*y3)+wt2*(yz22*y2+yz23*y3+yz24*y4)
endif

return
end
